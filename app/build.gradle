apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'let'

def keystorePropertiesFile = rootProject.file("keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "de.mchllngr.androidboilerplate"
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    // Stop build on stopship-comment (only in release)
    lintOptions {
        abortOnError true
        fatal 'StopShip'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$rootProject.ext.SUPPORT_VERSION"
    compile "com.android.support:design:$rootProject.ext.SUPPORT_VERSION"

    compile "com.hannesdorfmann.mosby3:mvp:$rootProject.ext.MOSBY_VERSION"

    compile "com.jakewharton:butterknife:$rootProject.ext.BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.BUTTERKNIFE_VERSION"

    compile "com.google.dagger:dagger:$rootProject.ext.DAGGER2_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.DAGGER2_VERSION"

    compile "com.jakewharton.timber:timber:$rootProject.ext.TIMBER_VERSION"

    debugCompile "com.facebook.stetho:stetho:$rootProject.ext.STETHO_VERSION"

    debugCompile "io.palaima.debugdrawer:debugdrawer:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugCompile "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugCompile "io.palaima.debugdrawer:debugdrawer-actions:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugCompile "io.palaima.debugdrawer:debugdrawer-location:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugCompile "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugCompile "io.palaima.debugdrawer:debugdrawer-fps:$rootProject.ext.DEBUGDRAWER_VERSION"
    // Used by DebugDrawer
    debugCompile "com.google.android.gms:play-services-location:$rootProject.ext.PLAY_SERVICES_LOCATION_VERSION"
    debugCompile "com.jakewharton.scalpel:scalpel:$rootProject.ext.SCALPEL_VERSION"
    debugCompile "jp.wasabeef:takt:$rootProject.ext.TAKT_VERSION"

    testCompile "junit:junit:$rootProject.ext.JUNIT_VERSION"
    testCompile "org.mockito:mockito-core:$rootProject.ext.MOCKITO_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
